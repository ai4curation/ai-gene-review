set shell := ["bash", "-eu", "-o", "pipefail", "-c"]

analysis_root := "results"

default: all

setup:
    uv sync

collect dataset outdir:
    mkdir -p {{outdir}}
    uv run scripts/collect_proteins.py \
      --input-tsv {{dataset}} \
      --output-json {{outdir}}/proteins.json \
      --output-fasta {{outdir}}/proteins.fasta \
      --output-summary-tsv {{outdir}}/protein_summary.tsv \
      --cache-dir data/uniprot_txt

sequence outdir:
    uv run scripts/analyze_sequence_features.py \
      --proteins-json {{outdir}}/proteins.json \
      --output-dir {{outdir}}/sequence

alphafold outdir:
    uv run scripts/download_alphafold.py \
      --proteins-json {{outdir}}/proteins.json \
      --output-dir {{outdir}}/structures

structure outdir:
    uv run scripts/analyze_structure_cysteines.py \
      --proteins-json {{outdir}}/proteins.json \
      --manifest-json {{outdir}}/structures/alphafold_manifest.json \
      --output-dir {{outdir}}/structure

run dataset outdir:
    just collect {{dataset}} {{outdir}}
    just sequence {{outdir}}
    just alphafold {{outdir}}
    just structure {{outdir}}

main:
    just run inputs/proteins.tsv {{analysis_root}}/main

test:
    just run inputs/proteins-test-tpx1.tsv {{analysis_root}}/test_tpx1

all: setup main test

clean:
    rm -rf {{analysis_root}}/main {{analysis_root}}/test_tpx1

prx5-panel outdir:
    mkdir -p {{outdir}}/phylogeny
    uv run scripts/build_prx5_panel.py \
      --output-tsv {{outdir}}/phylogeny/prx5_panel.tsv \
      --target-accession O14313 \
      --max-results 120 \
      --min-length 130 \
      --max-length 260

prx5-collect outdir:
    uv run scripts/collect_proteins.py \
      --input-tsv {{outdir}}/phylogeny/prx5_panel.tsv \
      --output-json {{outdir}}/phylogeny/prx5_proteins.json \
      --output-fasta {{outdir}}/phylogeny/prx5_proteins.fasta \
      --output-summary-tsv {{outdir}}/phylogeny/prx5_protein_summary.tsv \
      --cache-dir data/uniprot_txt

prx5-phylogeny outdir:
    uv run scripts/analyze_prx5_phylogeny.py \
      --proteins-json {{outdir}}/phylogeny/prx5_proteins.json \
      --target-id pmp20_o14313 \
      --neighbor-count 12 \
      --output-dir {{outdir}}/phylogeny

dimer-template outdir:
    uv run scripts/analyze_prx5_dimer_template.py \
      --proteins-json {{outdir}}/proteins.json \
      --target-id pmp20_schpo \
      --template-id prx5_o43099 \
      --template-pdb-id 5J9B \
      --output-dir {{outdir}}/dimer

main-extra:
    just prx5-panel {{analysis_root}}/main
    just prx5-collect {{analysis_root}}/main
    just prx5-phylogeny {{analysis_root}}/main
    just dimer-template {{analysis_root}}/main
